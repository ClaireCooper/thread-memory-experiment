plugins {
    id 'java'
    id 'application'
//    id 'com.bmuschko.docker-java-application' version '6.7.0'
    id 'com.bmuschko.docker-remote-api' version '6.7.0'
}
import com.bmuschko.gradle.docker.tasks.image.*
import com.bmuschko.gradle.docker.tasks.container.*
import com.github.dockerjava.api.exception.NotModifiedException

application {
    group = 'net.portswigger.academy'
    version = '1.0-SNAPSHOT'
    mainClassName = 'net.portswigger.academy.ThreadPoolExperiment'
}
def ourContainerName = "thread-pool-experiment-gradle-docker-container"
repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'net.portswigger.academy.ThreadPoolExperiment'
    }
    archiveBaseName = 'threadMemoryExperiment'
    archiveVersion =  '1.0-SNAPSHOT'
}

task createDockerFile(type: Dockerfile) {
    from 'openjdk:8-jre-alpine'
    copyFile jar.archiveFileName.get(), '/net/portswigger/academy/ThreadMemoryExperiment.jar'
    entryPoint 'java'
    defaultCommand '-jar', '/net/portswigger/academy/ThreadMemoryExperiment.jar'
    environmentVariable 'JAVA_OPTS', '-Xms1009m -Xmx1009m -Xss1m -XX:+UnlockExperimentalVMOptions -XX:+UseZGC'
}


task syncJar(type: Copy) {
    dependsOn assemble
    from jar.destinationDirectory
    into "$buildDir/docker"
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerFile, syncJar
    inputDir = createDockerFile.getDestDir()
    images = ["clairecooper/thread-pool-experiment-gradle-docker:1.0"]
}

task stopContainer(type: DockerStopContainer) {
    targetContainerId("$ourContainerName")
    onError { exception ->
        if (exception instanceof NotModifiedException) {
            println 'Container not running'
        } else if (exception.message != null && exception.message.contains('No such container')) {
            println 'Container not running'
        } else {
            throw exception
        }
    }
}

task removeContainer(type: DockerRemoveContainer) {
    dependsOn stopContainer
    targetContainerId("$ourContainerName")
    onError { exception -> handleError(exception) }
}

task createContainer(type: DockerCreateContainer) {
    dependsOn buildImage, removeContainer
    targetImageId buildImage.getImageId()

    containerName = "$ourContainerName"
    hostConfig.memory = Integer.toUnsignedLong(1024*1024*1024);
    hostConfig.memorySwap = Integer.toUnsignedLong(1024*1024*1024);
}

task startContainer(type: DockerStartContainer) {
    dependsOn createContainer
    targetContainerId("$ourContainerName")
}

private void handleError(Throwable exc) {
    if (exc.message != null &&
            !exc.message.contains('NotModifiedException') &&
            !exc.message.contains('No such container')) {
        throw new RuntimeException(exc)
    }
}